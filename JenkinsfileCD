pipeline {
  agent any

  environment {
    HELM_RELEASE = 'myapp'
    NAMESPACE = 'default'
    NOTIFY_EMAIL = 'devops.akash01@gmail.com'
  }

  stages {
    stage('Fetch Terraform & CI Outputs') {
      steps {
        copyArtifacts(projectName: 'Terraform-Infra-Pipeline', selector: lastSuccessful())
        copyArtifacts(projectName: 'NodeJs-Automation', selector: lastSuccessful())

        script {
          def tfData = readJSON file: 'tf_outputs.json'
          def ciData = readJSON file: 'image_info.json'

          env.GKE_CLUSTER = tfData.gke_cluster_name.value
          env.GCP_REGION = tfData.gcp_region.value
          env.PROJECT_ID = tfData.project_id.value
          env.IMAGE_NAME = ciData.image

          echo "Using image: ${env.IMAGE_NAME}"
          echo "Using GKE Cluster: ${env.GKE_CLUSTER} in Region: ${env.GCP_REGION}"
        }
      }
    }

    stage('Configure Kubeconfig') {
      steps {
        sh '''
          gcloud container clusters get-credentials $GKE_CLUSTER \
            --region $GCP_REGION \
            --project $PROJECT_ID
        '''
      }
    }

    stage('Deploy with Helm') {
      steps {
        script {
          try {
            sh """
              helm upgrade --install ${env.HELM_RELEASE} ./helm/myapp \
                --set image.repository=${env.IMAGE_NAME.split(":")[0]} \
                --set image.tag=${env.IMAGE_NAME.split(":")[1]} \
                --namespace ${env.NAMESPACE}
            """
            echo "Helm deployment succeeded."
          } catch (err) {
            echo "Helm deployment failed: ${err}"
            echo "Attempting rollback to previous release..."
            sh "helm rollback ${env.HELM_RELEASE}"
            error("Deployment failed and was rolled back.")
          }
        }
      }
    }

    stage('Test Helm Release') {
      steps {
        script {
          try {
            sh "helm test ${env.HELM_RELEASE} --timeout 60s"
            echo "Helm test passed."
          } catch (err) {
            echo "Helm test failed. Rolling back..."
            sh "helm rollback ${env.HELM_RELEASE}"
            error("Helm test failed and rollback triggered.")
          }
        }
      }
    }
  }
}
